USE [master]
GO

SET NOCOUNT ON

/*Generate an execution script for each Job that needs the owner changed*/
DECLARE @LoopExecuteScripts TABLE (exec_command VARCHAR(1000))

INSERT INTO @LoopExecuteScripts
SELECT 'msdb.dbo.sp_update_job @job_id=N'''+CAST(job_id AS VARCHAR(150))+''', @owner_login_name=N''sa'' '
FROM msdb.dbo.sysjobs J
INNER JOIN msdb.dbo.syscategories C ON C.category_id = J.category_id
WHERE msdb.dbo.SQLAGENT_SUSER_SNAME(j.owner_sid) <> 'sa';

/*Loop through each record in @LoopExecuteScripts table and execute the command*/
DECLARE @Current_Record VARCHAR(1000)

SELECT @Current_Record = MIN(exec_command) FROM @LoopExecuteScripts

WHILE @Current_Record IS NOT NULL
BEGIN
	PRINT @Current_Record
	EXEC (@Current_Record)
	--Delete processed record
	DELETE FROM @LoopExecuteScripts WHERE exec_command = @Current_Record
	--Get next record to be processed
	SELECT @Current_Record = MIN(exec_command) FROM @LoopExecuteScripts
END